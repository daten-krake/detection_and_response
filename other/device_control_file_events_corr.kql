// Correlate USB policy triggers with file creations on removable storage
let PolicyEventsDrive = //Creates target Drive
    DeviceEvents
    | where ActionType == "RemovableStoragePolicyTriggered"
    | where FolderPath !has "N/A"
    | extend drive = split(FolderPath, '\\')
    | extend  DriveName = tostring(drive[0])
    | summarize by DriveName;
let PolicyEvents = // Checks for write events to external Storage
    DeviceEvents
    | where ActionType == "RemovableStoragePolicyTriggered"
    | where FolderPath !has "N/A"
    | extend action = todynamic(AdditionalFields).RemovableStorageAccess
    | where action contains "write" 
    | extend User = tostring(parse_json(AdditionalFields).AccountName)
    | extend  PolicyAction = tostring(parse_json(AdditionalFields).RemovableStoragePolicyVerdict);
let FileEvents = 
    DeviceFileEvents // Used for file Creation Events to see File  Details
    | where ActionType == "FileCreated"
    | extend drive = split(FolderPath, '\\')
    | extend DriveName = tostring(drive[0])
    | where DriveName in (PolicyEventsDrive)
    | project
    Timestamp, 
        DeviceId,
        DriveName,
        FileName,
        FolderPath,
        FileAccount = InitiatingProcessAccountName,
        SourcePath = strcat(InitiatingProcessFolderPath, InitiatingProcessFileName);
PolicyEvents // Join everything togehter
| join FileEvents on DeviceId
| project Timestamp, DeviceId, ActionType, WrittenTo=FolderPath1, DeviceName, FileAccount, WrittenFrom=SourcePath, File=FileName1
